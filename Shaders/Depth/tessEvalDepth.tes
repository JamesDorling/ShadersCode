#version 450 core
layout(triangles, equal_spacing, ccw) in;

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//SHADER IS ESSENTIALLY THE SAME AS THE "tessEval.tes" SHADER, HOWEVER WITH A LOT TAKEN OUT AS IT WAS UNECESSARY.
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

vec2 interpolate2D(vec2 v0, vec2 v1, vec2 v2);
vec3 interpolate3D(vec3 v0, vec3 v1, vec3 v2);
float hash(float n);
float snoise(vec3 x);
float noise(vec3 position, int octaves);

uniform mat4 view;
uniform mat4 projection;

uniform vec4 lightSpaceMatrix;

uniform int terrMode;

uniform sampler2D heightMap;
uniform int scale;

uniform vec3 camPos;

uniform int octaves;

in TC_OUT
{
	vec2 tcUV;
	vec3 tcFragPos;
}te_in[];

out TE_OUT
{
	vec3 teFragPos;
	vec2 teUV;
}te_out;

void main()
{
	//NON PN TRIANGLES
	te_out.teFragPos = interpolate3D(te_in[0].tcFragPos, te_in[1].tcFragPos, te_in[2].tcFragPos);

	te_out.teUV = interpolate2D(te_in[0].tcUV, te_in[1].tcUV, te_in[2].tcUV);

	if(terrMode != 1)
	{
		//Normal Calculations
		float right = (textureOffset(heightMap, te_out.teUV, ivec2(1, 0)).r)*scale;
		float left = (textureOffset(heightMap, te_out.teUV, ivec2(-1, 0)).r)*scale;
		float up = (textureOffset(heightMap, te_out.teUV, ivec2(0, 1)).r)*scale;
		float down = (textureOffset(heightMap, te_out.teUV, ivec2(0, -1)).r)*scale;
		
		float lr = left - right;
		float du = up - down;

		//HEIGHT MAP STUFF
		float height;
		height = texture(heightMap, te_out.teUV).r; //this gives a value in a [0,1] range
		te_out.teFragPos.y = height * scale;
	}
	else if(terrMode == 1)
	{
	    //WORK OUT NORMALS
		te_out.teFragPos.y = noise(te_out.teFragPos, octaves);
	}
	

	//NECCESSARY EITHER WAY
	gl_Position = projection * view * vec4(te_out.teFragPos, 1.0); 
}

vec2 interpolate2D(vec2 v0, vec2 v1, vec2 v2)
{
	return vec2(gl_TessCoord.x) * v0 + vec2(gl_TessCoord.y) * v1 + vec2(gl_TessCoord.z) * v2;
}

vec3 interpolate3D(vec3 v0, vec3 v1, vec3 v2)
{
	return vec3(gl_TessCoord.x) * v0 + vec3(gl_TessCoord.y) * v1 + vec3(gl_TessCoord.z) * v2;
}

float hash(float n) 
{
    return fract(sin(n) * 753.5453123);
}

float snoise(vec3 x)
{
	vec3 p = floor(x);
	vec3 f = fract(x);
	f = f * f * (3.0 - (2.0 * f));

	float n = p.x + p.y * 157.0 + 113.0 * p.z;
	return mix(mix(mix(hash(n + 0.0), hash(n + 1.0), f.x),
		mix(hash(n + 157.0), hash(n + 158.0), f.x), f.y),
		mix(mix(hash(n + 113.0), hash(n + 114.0), f.x),
			mix(hash(n + 270.0), hash(n + 271.0), f.x), f.y), f.z);
}

float noise(vec3 position, int octaves)
{
	float total = 0.0;
	float frequency = 0.007;
	float maxAmplitude = 0.0;
	float amplitude = 10.0;
	for (int i = 0; i < octaves; i++)
	{
		total += snoise(position * frequency) * amplitude;
		frequency *= 2.0;
		amplitude /= 2.0;
		maxAmplitude += amplitude;
	}
	return (total / maxAmplitude) * scale;
}
